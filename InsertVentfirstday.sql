CREATE PROCEDURE [dbo].[InsertVentfirstday]
AS

-- Checked for DATEADD

-- Determines if a patient is ventilated on the first day of their ICU stay.
-- Creates a table with the result.
-- Requires the `ventdurations` table, generated by ../ventilation-durations.sql

DROP TABLE IF EXISTS dbo.ventfirstday;
select
  ie.subject_id, ie.hadm_id, ie.icustay_id
  -- if vd.icustay_id is not null, then they have a valid ventilation event
  -- in this case, we say they are ventilated
  -- otherwise, they are not
  , max(case when vd.icustay_id is not null then 1 else 0 end) as vent
into dbo.ventfirstday
from icustays ie
left join ventdurations vd
  on ie.icustay_id = vd.icustay_id
  and
  (
    -- ventilation duration overlaps with ICU admission -> vented on admission
    (vd.starttime <= ie.intime and vd.endtime >= ie.intime)
    -- ventilation started during the first day
    OR (vd.starttime >= ie.intime 
	    and vd.starttime <= DATEADD(DAY, 1, ie.intime))
  )
group by ie.subject_id, ie.hadm_id, ie.icustay_id
order by ie.subject_id, ie.hadm_id, ie.icustay_id;
GO
